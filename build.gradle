plugins {
    //This is a handy plugin as it rolls everything into a single Jar file
    //See https://github.com/johnrengelman/shadow/
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'application'
apply plugin: 'build-dashboard'

//The idea and eclipse plugins provide helpers for 2 popular IDEs
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    //The JCenter repository holds libraries that we can use in our code
    //See: https://bintray.com/bintray/jcenter
    jcenter()
}

group 'org.groovytutorial.shapes'
version '1.0-SNAPSHOT'

dependencies {
    //This brings in the Groovy system:
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: "$project.groovyVersion"

    //This brings in the Spock Testing Framework for the test stage
    testCompile group: 'org.spockframework', name: 'spock-core', version: "$project.spockVersion"
}

//This is the class that is called when using the "run" task
mainClassName = 'org.groovytutorial.shapes.app.Main'

//CodeNarc analyses the source code for possible imporvements
codenarc.toolVersion = "$project.codeNarcVersion"
codenarcMain {
    ignoreFailures false
    configFile file('config/codenarc/ruleset.groovy')
    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 20
}

codenarcTest {
    ignoreFailures true
    configFile file('config/codenarc/ruleset.groovy')
    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 20
}

//The Gradle wrapper makes it easy for others to try out your code
task wrapper(type: Wrapper) {
    gradleVersion = "$project.gradleVersion"
}
