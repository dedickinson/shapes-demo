plugins {
    id 'groovy'
    id 'application'
    id 'build-dashboard'
    id 'project-report'
    id 'codenarc'

    //This is a handy plugin as it rolls everything into a single Jar file: https://github.com/johnrengelman/shadow/
    id 'com.github.johnrengelman.shadow' version '1.2.2'

    //The idea and eclipse plugins provide helpers for 2 popular IDEs
    id 'eclipse'
    id 'idea'

    //These plugins are used to generate this tutorial
    id "org.asciidoctor.gradle.asciidoctor" version "1.5.1"
    id "org.ajoberstar.github-pages" version "1.3.0"
}

repositories {
    //The JCenter repository holds libraries that we can use in our code: https://bintray.com/bintray/jcenter
    jcenter()
}

project.ext {
    groovyVersion = '2.4.4'
    spockVersion = '1.0-groovy-2.4'
    gradleVersion = '2.7'
    codeNarcVersion = '0.24'
}

//project.name is set in the settings.gradle file using: rootProject.name = 'shapes-demo'
project.with {
    group 'org.groovytutorial.shapes'
    version '1.0-SNAPSHOT'
    description = 'A tutorial covering basic Gradle usage and OO-development in Groovy'

    defaultTasks 'run'

    //This is the class that is called when using the "run" task
    mainClassName = 'org.groovytutorial.shapes.app.Main'

    wrapper.gradleVersion = "$gradleVersion"
}


dependencies {
    //This brings in the Groovy system:
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: "$groovyVersion"

    //This brings in the Spock Testing Framework for the test stage
    testCompile group: 'org.spockframework', name: 'spock-core', version: "$spockVersion"
}

//CodeNarc analyses the source code for possible improvements
codenarc {
    toolVersion = "$codeNarcVersion"

    codenarcMain {
        configFile file('config/codenarc/ruleset.groovy')
        exclude "**/package-info.*"
        ignoreFailures false
        maxPriority1Violations 0
        maxPriority2Violations 10
        maxPriority3Violations 20
    }

    codenarcTest {
        ignoreFailures true
        configFile file('config/codenarc/ruleset-test.groovy')
        maxPriority1Violations 0
        maxPriority2Violations 10
        maxPriority3Violations 20
    }
}

/*
 * All items below here are used for preparing the distributable tutorial.
 * You could delete everything that follows if you'd like and you'd get
 * all of the Gradle defaults
 */
asciidoctor {
    logDocuments = true
    separateOutputDirs = false
}


task syncPublishFiles(type: Sync, dependsOn: [ buildDashboard, build, shadowJar, check,
                                               groovydoc, asciidoctor, projectReport ]) {
    description 'Collates all of the required files for publishing to GitHub pages branch'
    group 'publishing'

    mustRunAfter clean
    into "$buildDir/publish-site"

    from(reporting.baseDir) {
        into 'reports'
    }
    from asciidoctor.outputDir
    from(docsDir) {
        into 'api'
    }
    from(distsDir) {
        into 'dist'
    }
    from(shadowJar.destinationDir) {
        include shadowJar.archiveName
        into 'dist'
    }
    from(projectDir) {
        includes = [ 'build.gradle',
                     'settings.gradle',
                     'gradle.properties',
                     'gradlew',
                     'gradlew.bat',
                     'gradle/**' ]
        into 'dev'
    }
    from('src') {
        into 'dev/src'
        exclude 'docs'
    }
    from 'LICENSE'
}

//See: https://github.com/ajoberstar/gradle-git/wiki/org.ajoberstar.github-pages
apply plugin: 'org.ajoberstar.github-pages'
githubPages {
    repoUri = 'git@github.com:groovy-tutorial/shapes-demo.git'
    pages {
        from "$buildDir/publish-site"
    }
    publishGhPages {
        dependsOn syncPublishFiles
    }
}
